apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply from: "$project.rootDir/buildscripts/versions.gradle"

android {
    compileSdk 32

    defaultConfig {
        applicationId "hu.mostoha.mobile.android.huki"
        minSdk 26
        targetSdk 32
        versionCode gitVersionCode
        versionName gitVersionName

        testInstrumentationRunner "hu.mostoha.mobile.android.huki.HiltTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            manifestPlaceholders = [
                    appName: "@string/app_name_debug",
                    appIcon: "@mipmap/ic_launcher_debug"
            ]
            archivesBaseName = "HuKi_${defaultConfig.versionName}_${defaultConfig.versionCode}"
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
        release {
            manifestPlaceholders = [
                    appName: "@string/app_name",
                    appIcon: "@mipmap/ic_launcher"
            ]
            archivesBaseName = "HuKi_${defaultConfig.versionName}_${defaultConfig.versionCode}"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['src/main/res', 'src/main/res_symbols']
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        packagingOptions {
            jniLibs {
                useLegacyPackaging true
            }
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/AL2.0', 'META-INF/LGPL2.1']
        }
    }

    lint {
        abortOnError true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':osm-overpasser')

    def kotlinxCoroutinesVersion = "1.6.4"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"
    implementation "androidx.appcompat:appcompat:1.5.0"

    // Hilt
    def jetpackHiltVersion = "1.0.0"
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    kapt "androidx.hilt:hilt-compiler:$jetpackHiltVersion"

    // KTX
    implementation "androidx.core:core-ktx:1.8.0"
    def activityKtxVersion = "1.5.1"
    implementation "androidx.activity:activity-ktx:$activityKtxVersion"
    def fragmentKtxVersion = "1.5.2"
    implementation "androidx.fragment:fragment-ktx:$fragmentKtxVersion"
    def lifecycleVersion = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    // Google Play Services Location
    implementation "com.google.android.gms:play-services-location:20.0.0"

    // OSM
    implementation "org.osmdroid:osmdroid-android:6.1.13"

    // Network
    def moshiVersion = "1.13.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.6"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // AWS
    def awsVersion = "1.37.2"
    implementation "com.amplifyframework:core:$awsVersion"
    implementation "com.amplifyframework:aws-storage-s3:$awsVersion"
    implementation "com.amplifyframework:aws-auth-cognito:$awsVersion"

    // Design
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.material:material:1.6.1"

    // Logging
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Analytics + Crashlytics
    implementation "com.google.firebase:firebase-analytics-ktx:21.1.0"
    implementation "com.google.firebase:firebase-crashlytics-ktx:18.2.12"

    // Licenses
    implementation 'com.github.marcoscgdev:Licenser:2.0.0'

    // Unit tests
    testImplementation project(":test-data")

    def mockKVersion = '1.12.5'
    def kotlinCoroutineTestVersion = '1.6.4'
    testImplementation "junit:junit:4.13.2"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "io.mockk:mockk:$mockKVersion"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutineTestVersion"
    testImplementation 'app.cash.turbine:turbine:0.9.0'

    // Instrumentation tests
    androidTestImplementation project(":test-data")

    def androidTestVersion = "1.4.0"
    def androidExtTestVersion = "1.1.3"
    def espressoVersion = "3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "com.google.truth:truth:1.1.3"
    androidTestImplementation "androidx.test.ext:junit:$androidExtTestVersion"
    androidTestImplementation "androidx.test.ext:junit-ktx:$androidExtTestVersion"
    androidTestImplementation "androidx.test:core-ktx:$androidTestVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutineTestVersion"

    androidTestImplementation "io.mockk:mockk-android:$mockKVersion"

    androidTestImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hiltVersion"

    androidTestUtil "androidx.test:orchestrator:1.4.1"
}

kapt {
    correctErrorTypes true
}
